apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'macAppBundle'
apply plugin: 'spring-boot'

def runnerClassName = "com.github.zutherb.buildlight.application.Runner"

mainClassName = runnerClassName

macAppBundle {
    mainClassName = "${runnerClassName}"
    icon = "${buildDir}/resources/main/icons/TrafficLight.icns"
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.zutherb.gradle:gradle-macappbundle:0.1'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.1.3.RELEASE"
    }
}

group = 'com.github.zutherb.buildlight.'

sourceSets {
    main {
        resources {
            srcDir 'src/main/java'
            srcDir 'src/main/groovy'
            srcDir 'src/main/resources'
        }
    }
    test {
        resources {
            srcDir 'src/test/java'
            srcDir 'src/test/resources'
        }
    }
}

dependencies {
    compile project(":driver:cleware-driver")
    compile project(":driver:arduino-driver")
    compile project(":driver:blinkstick-driver")

    compile project(":repository:bamboo:api")
    compile project(":repository:bamboo:model")
    compile project(":repository:bamboo:impl")

    compile project(":repository:jenkins:api")
    compile project(":repository:jenkins:model")
    compile project(":repository:jenkins:impl")

    compile commonDeps
    compile springDeps
    compile javaFxDeps

    compile "org.springframework:spring-web:$springVersion"
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "org.reflections:reflections:0.9.8"

    testCompile testDeps
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Build Light Application",
                "Implementation-Version": "${version}",
                "Author": "Bernd Zuther <bernd.zuther@me.com>",
                "Main-Class": "${runnerClassName}"
        )
    }
}

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}